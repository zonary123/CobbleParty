plugins {
    // The shadow gradle plugin assists with repackaging a 3rd-party library jar within your own jar
    // In addition to ensuring that your end-users have the library available when they use your own thing,
    // it also helps avoid collisions with other things that are also using the same library.
    // ref: https://gist.github.com/SizableShrimp/949e7c219bfc94487a45226b64ac7749
    //
    // Latest Test Compatibility: gradle version 8.x shadow version 8.0.0+
    // ref: https://github.com/johnrengelman/shadow
    id "com.gradleup.shadow" version "8.3.5"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    // Loom sets the log4j config while in development (in prod the log4j config might be dictated by the launcher or just the game itself).
    // This loom gradle config specifies that loom uses our customized log4j config file.
    // ref: https://fabricmc.net/wiki/documentation:fabric_loom
    // (although we're using the architectury fork of fabric loom, this config remains unchanged)
    log4jConfigs.from(file("../common/src/main/resources/log4j.xml"))
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

// since 1.20.6
//configurations.configureEach {
//    resolutionStrategy.force("net.sf.jopt-simple:jopt-simple:5.0.4")
//
//}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    modImplementation include("dev.architectury:architectury-neoforge:${project.architectury_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }

    // https://github.com/LlamaLad7/MixinExtras
    // fabric has this dependency,
    // we already used this dependency in common module,
    // so let forge use it too
//    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.0"))
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        Map data = Map.of(
                "mod_name", project.property("mod_name"),
                "mod_id", project.property("mod_id"),
                "mod_version", project.property("mod_version"),
                "mod_description", project.property("mod_description"),

                "author", project.property("author"),
                "repository", project.property("repository"),
                "license", project.property("license"),
                "mod_icon", project.property("mod_icon"),
                "neoforge_version", rootProject.property("neoforge_version"),
                "supported_minecraft_versions", project.property("supported_minecraft_versions")
        )
        expand data
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

